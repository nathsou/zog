
let print: a => () = raw { console.log }
let iter: a[] => Iterator<a> = raw { array => array[Symbol.iterator]() }
let push: (a[], a) => () = raw { (array, elem) => array.push(elem) }

let isPrime = (sieve, n) => {
    for p in sieve->iter {
        if p * p > n {
            return true
        }
            
        if n % p == 0 {
            return false
        }
    }
    
    return true
}

let primes = iterator max => {
    yield 2
    
    mut sieve = [2]
    mut n = 3
    
    while n <= max {
        if isPrime(sieve, n) {
            sieve->push(n)
            yield n
        }
        
        n += 2
    }
}

let count = it => {
    mut len = 0
    
    for elem in it {
        len += 1
    }
    
    len
}

print(primes(1000000)->count)
